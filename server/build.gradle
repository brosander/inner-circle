plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.40'
    id 'application'
    id "com.palantir.docker" version "0.22.1"
}

group 'com.github.brosander.innercircle'
version '0.0.1-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    def ktorVersion = '1.2.2'
    def jacksonVersion = '2.9.9'

    implementation "ch.qos.logback:logback-classic:1.2.3"
    
    implementation 'com.amazonaws:aws-java-sdk-s3:1.11.546'

    implementation 'com.google.inject:guice:4.2.2'

    implementation "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
    implementation "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:$jacksonVersion"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"

    implementation 'commons-cli:commons-cli:1.4'

    implementation "io.ktor:ktor-auth:$ktorVersion"
    implementation "io.ktor:ktor-client-apache:$ktorVersion"
    implementation "io.ktor:ktor-server-netty:$ktorVersion"
    implementation "io.ktor:ktor-jackson:$ktorVersion"
    implementation "io.ktor:ktor-html-builder:$ktorVersion"
    implementation "io.ktor:ktor-locations:$ktorVersion"
    implementation "io.ktor:ktor-velocity:$ktorVersion"

    implementation 'org.apache.commons:commons-text:1.6'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlinx:kotlinx-html-jvm:0.6.12"
    
    implementation 'org.postgresql:postgresql:42.2.5'
    
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

mainClassName = 'com.github.brosander.innercircle.InnerCircleKt'

startScripts.with {
    doLast {
        unixScript.text = unixScript.text.replace('APP_HOME=', 'export APP_HOME=')
        windowsScript.text = windowsScript.text
    }
}

task prepareStaticAssets(type: Sync) {
    dependsOn(':client:build')

    from '../client/build/dist-output'
    into 'build/static-assets'
}

distributions {
    main {
        contents {
            from(prepareStaticAssets) {
                into 'static'
            }
            from('src/main/config') {
                into 'config'
            }
        }
    }
}

docker {
    name "inner-circle-${project.name}"
    tags version
    files tasks.installDist.outputs
}